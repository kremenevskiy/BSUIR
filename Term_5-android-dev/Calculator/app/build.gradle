plugins {
    id 'com.android.application'
    id 'kotlin-android'
}


def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'android_lab1', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', 'android_lab1', '--tags'
            standardOutput = stdout
        }
        def lastTag = stdout.toString().trim()
        return lastTag.replaceAll('v|-\\w+$', '').replace('-', '.')
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdk 31

    buildFeatures {
        viewBinding true
    }




    def BASE_URL = "BASE_URL"
    flavorDimensions "dev"

    productFlavors {
        free {
            dimension "dev"
            applicationIdSuffix ".free"
            buildConfigField "String", BASE_URL, '"https://freeServer.com"'
        }

        paid {
            dimension "dev"
            applicationIdSuffix ".paid"
            buildConfigField "String", BASE_URL, '"https://paidServer.com"'
        }
    }

    defaultConfig {
        applicationId "com.example.calculator"
        minSdk 23
        targetSdk 31
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    

    buildTypes {
        debug {
            testCoverageEnabled = true
            minifyEnabled true
        }
        release {
            testCoverageEnabled = false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation files('libs/MathParser.org-mXparser-v.4.4.0-jdk11.jar')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}